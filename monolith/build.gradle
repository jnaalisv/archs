plugins {
    id 'org.springframework.boot' version '1.5.2.RELEASE'
    id 'java'
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    // Logging dependency versions
    slf4jVersion = "1.7.25"
    logbackVersion = "1.2.3"
    groovyVersion = "2.4.9"

    // Spring Framework dependency versions
    springVersion = "4.3.8.RELEASE"

    // Data access dependency versions
    hibernateVersion = "5.2.10.Final"
    hikaricpVersion = "2.6.2"
    postgreSqlVersion = "42.1.1"

    // Test dependency versions
    h2Version = "1.4.194"
    junitVersion = "4.12"
    assertJVersion = "3.8.0"

    flywayVersion = "4.2.0"

    logback_runtime = [
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.slf4j:log4j-over-slf4j:$slf4jVersion",
            "org.slf4j:jul-to-slf4j:$slf4jVersion",
            "org.codehaus.groovy:groovy:$groovyVersion",
            "ch.qos.logback:logback-classic:$logbackVersion"
    ]
}

// override spring managed dependencies
ext['hibernate.version'] = ext.hibernateVersion
ext['hikaricp.version'] = ext.hikaricpVersion
ext['assertj.version'] = ext.assertJVersion

dependencies {

    runtime "org.codehaus.groovy:groovy:$groovyVersion",
            "org.postgresql:postgresql:$postgreSqlVersion"

    compile project(':model'),
            project(':persistence'),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework:spring-tx"

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

sourceSets {
    test {
        resources {
            srcDirs += "$rootDir/shared/test/resources"
            srcDirs += "$rootDir/shared/logback"
        }
    }
}

wrapper {
    gradleVersion = "3.5"
}
